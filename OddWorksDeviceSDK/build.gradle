apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }
}

group = 'io.oddworks'
version = '1.0.0-beta-48'

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'com.squareup:otto:1.3.8'
    compile 'net.danlew:android.joda:2.9.1'

    androidTestCompile 'com.android.support.test:runner:0.4'
    androidTestCompile 'com.android.support.test:rules:0.4'
    androidTestCompile 'org.skyscreamer:jsonassert:1.2.3'
    androidTestCompile 'org.hamcrest:hamcrest-core:1.3'
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJar(MavenPublication) {
            groupId = "$group"
            artifactId = "device-sdk"
            version = "$version"

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier 'javadoc'
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJar']
    pkg {
        repo = 'oddworks'
        name = 'device-sdk'
        userOrg = 'oddnetworks'
        licenses = ['Apache-2.0']
        desc = 'Oddworks SDK for Android'
        websiteUrl = 'http://android.guide.oddworks.io'
        vcsUrl = 'https://github.com/oddnetworks/OddworksDeviceSDK.git'
    }
}
